# vim:set et sw=2 ts=8 ft=zsh:

local base lbuf compgen fzf_opts suffix tail dir leftover matches
# (Q) flag removes a quoting level: "foo\ bar" => "foo bar"
base=${(Q)1}
lbuf=$2
compgen=$3
fzf_opts=$4
suffix=$5
tail=$6

setopt localoptions nonomatch
dir="$base"
while [ 1 ]; do
  if [[ -z "$dir" || -d ${~dir} ]]; then
    leftover=${base/#"$dir"}
    leftover=${leftover/#\/}
    [ -z "$dir" ] && dir='.'
    [ "$dir" != "/" ] && dir="${dir/%\//}"
    dir=${~dir}
    matches=$(eval "$compgen $(printf %q "$dir")" | fzf-tmux --height 40% --reverse ${=fzf_opts} -q "$leftover" | while read item; do
      echo -n "${(q)item}$suffix "
    done)
    matches=${matches% }
    if [ -n "$matches" ]; then
      LBUFFER="$lbuf$matches$tail"
    fi
    zle redisplay
    typeset -f zle-line-init >/dev/null && zle zle-line-init
    break
  fi
  dir=$(dirname "$dir")
  dir=${dir%/}/
done
